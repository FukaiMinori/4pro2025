precision highp float;                 // 浮動小数点精度を高く設定（演算誤差を低減）

uniform float u_time;                  // 経過時間
uniform vec2  u_resolution;            // 画面解像度（幅, 高さ）
uniform float radius;                  // 半径（最終的に色のゆらぎで使用）
uniform float num;                     // 球面上の点の総数
uniform float an;                      // カメラの回転角
out vec4 fragColor;                    // 出力するピクセルの最終 RGBA 値

//---------------------------------------------------------------
// Inigo Quilez - Golden spiral sphere mapping demo
//---------------------------------------------------------------

// p: 球面上の法線ベクトル（単位ベクトル）
// 戻り値: 
//   x = 最も近い黄金螺旋点のインデックス
//   y = その点との距離
vec2 inverseSF( vec3 p ) { 
    const float kTau = 6.28318530718;       // 2π
    const float kPhi = (1.0 + sqrt(5.0)) / 2.0; // 黄金比 Φ

    //---------------------------
    // 層（k）を推定
    // 高さ p.z から黄金螺旋ライン上のどの層にいるかを対数式で近似
    //---------------------------
    float k  = max(
        2.0, 
        floor(
            log2(num * kTau * 0.5 * sqrt(5.0) * (1.0 - p.z * p.z))
            / log2(kPhi + 1.0)
        )
    );

    //---------------------------
    // フィボナッチ近似を使って F(k), F(k+1) を推定
    //---------------------------
    float Fk = pow(kPhi, k) / sqrt(5.0);
    vec2  F  = vec2(round(Fk), round(Fk * kPhi));   // (F(k), F(k+1))

    //---------------------------
    // 黄金螺旋グリッドの係数を生成
    //---------------------------
    vec2  ka = 2.0 * F / num;                       // 緯度方向の係数
    vec2  kb = kTau * (fract((F + 1.0) * kPhi) - (kPhi - 1.0)); // 経度方向の係数

    //---------------------------
    // 逆行列により点 p に最も近い格子番号 c を推定
    //---------------------------
    mat2 iB = mat2(ka.y, -ka.x, kb.y, -kb.x)
             / (ka.y * kb.x - ka.x * kb.y);

    vec2 c = floor(iB * vec2(
        atan(p.y, p.x),                  // 経度 φ
        p.z - 1.0 + 1.0 / num            // 緯度方向補正
    ));

    //---------------------------
    // 近傍の 4 点をチェックして最短距離の点を探す
    //---------------------------
    float d = 8.0;                       // 最短距離（初期値）
    float j = 0.0;                       // 最も近い点の index

    for (int s = 0; s < 4; s++) {
        vec2  uv = vec2(s & 1, s >> 1);  // (0,0),(1,0),(0,1),(1,1)
        float id = clamp(dot(F, uv + c), 0.0, num - 1.0);

        float phi = kTau * fract(id * kPhi);       // 経度
        float cosTheta = 1.0 - (2.0 * id + 1.0) / num; // 緯度 cosθ
        float sinTheta = sqrt(1.0 - cosTheta * cosTheta);

        vec3 q = vec3(
            cos(phi) * sinTheta,
            sin(phi) * sinTheta,
            cosTheta
        );

        float tmp = dot(q - p, q - p);  // 距離の二乗
        if (tmp < d) {
            d = tmp;
            j = id;
        }
    }

    return vec2(j, sqrt(d));            // 点 index と距離
}

//---------------------------------------------------------------
// 0~1 の乱数を返す簡易 hash 関数
//---------------------------------------------------------------
float hash1(float n) { 
    return fract(sin(n) * 158.5453123);
}

//---------------------------------------------------------------
// main: ピクセルごとの描画処理
//---------------------------------------------------------------
void main() {

    //---------------------------
    // ピクセル座標を正規化（中心を (0,0) に）
    //---------------------------
    vec2 fragCoord = gl_FragCoord.xy;
    vec2 p = (-u_resolution.xy + 2.0 * fragCoord.xy) / u_resolution.y;

    //---------------------------
    // カメラ設定
    //---------------------------
    //カメラ位置。anにより、カメラが円周上を回る。高さは1.0
    vec3 ro = vec3(2.5 * cos(an), 1.0, 2.5 * sin(an));
    vec3 ta = vec3(0.0, 1.0, 0.0); // 注視点。カメラはここに向かっている

    vec3 ww = normalize(ta - ro); //カメラの前方向(正規化したta-ro)
    vec3 uu = normalize(cross(ww, vec3(0.0, 1.0, 0.0))); //右方向ベクトル(wwとy軸との外積)
    vec3 vv = normalize(cross(uu, ww));  //上方向ベクトル(uuとwwの外積)
    //スクリーン上の座標p.xとp.yをカメラの右・上ベクトルに乗せ、前方向に1.5*wwを加えることで視野を作る
    //1.5は視野に相当する値
    vec3 rd = normalize(p.x * uu + p.y * vv + 1.5 * ww); 

    //---------------------------
    // 初期化
    //---------------------------
    vec3 sc = vec3(0.0, 1.0, 0.0); // 球の中心
    vec3 col = vec3(1.0);
    float tmin = 10000.0;         // 最小の交差距離(レイの最近接交差のt値(レイがどれだけ進んだか))
    //大きい初期値を設定しているのは、レイが何も当たらない場合を示すため
    vec3  nor = vec3(0.0);        //法線
    float occ = 1.0;              //影の係数
    vec3  pos = vec3(0.0);        //交差点


    //記号整理
    //ro = r0 = レイの始点(カメラ位置)
    //rd = レイの方向
    //sc = 球の中心C(vec3(0.0,1.0,0.0))
    //t(一時変数hを利用している) = レイパラメータ(距離)
    //tmin = 現在見つかっている最小の交差距離(小さいほど手前で交差している)
    //pos = 交差点位置 = ro + t*rd
    //nor = 交差点の法線(球ならnormalize(pos - sc)、平面なら固定)

    //---------------------------
    // 地面 y=0 との交差判定
    //---------------------------
    
    float h = (0.0 - ro.y) / rd.y;
    //tを解く式(平面y=0と交差するt)
    //交差点位置でのレイのy成分：ro.y + trd.y = 0→この式をtについて解くと、hの式になる。
    if (h > 0.0) { //レイが前方(カメラから見て正方向)に交差しているかの確認
        tmin = h; //最初の交差距離
        nor = vec3(0.0, 1.0, 0.0); //地面の法線(上向き)
        pos = ro + h * rd; //交点座標の計算
        vec3 di = sc - pos; //地面上の交点から球中心までのベクトル(球との距離や影処理に使用)
        float l = length(di); //diの長さ
        occ = 1.0 - dot(nor, di / l) / (l * l);    // 影の処理
        col = vec3(1.0);
    }
    /*
    ↑ y
   |
   |   camera (ro)
   |    *
   |     \
   |      \  rd  →  レイ方向
   |       \
   +--------+--------→ x
   y = 0  との交差  → t = (0 - ro.y) / rd.y
    */

    //---------------------------
    // 球との交差判定（2次方程式）
    //---------------------------
    /*球の方程式(半径R=1,中心sc)
        |pos-sc|^2 = 1
    レイを代入：pos(h) = ro + hrdより
        |ro + hrd - sc|^2 = 1
        |(ro - sc) + hrd|^2 = 1 (ここで、ro-scをceと置く)
    展開してtについての二次方程式にする
        (rd*rd)h^2 + 2(rd*ce)h + (ce*ce-1) =0
    rdが正規化されているからrd=1,rd*rd=1
    b = rd*ce
    c = ce*ce-1 と置く
        h^2 + 2bh + c = 0
    判別式D = (2b)^2 -4c = 4(b^2 - c)
    コードでは簡易化のためh =b^2-cを使用(実際はd=2√h)
    解はt = -b ± √b^2-c
    コードではh=b*b-c、次にh=-b - sqrt(h)として最も近い解をhに格納

    二つの解があるのはレイが球を入って出るから
    解はt1<t2(t1=-b - sqrt(h), t2=-b +　sqrt(h))
    解がどちらも負だった場合→レイの後方で交差している(無視)
    t1→レイが球と最初に当たる交差距離
    t2→レイが球を出る時の交差距離
    交点P = ro + trd
    球の法線は中心からの放射方向=(交点位置-球の中心)を正規化したもの
    */
    vec3  ce = ro - sc;
    float b = dot(rd, ce);
    float c = dot(ce, ce) - 1.0;   
    h = b * b - c;                 //判別式

    if (h > 0.0) {
        h = -b - sqrt(h);          //t1→レイが球と最初に当たる交差距離
        if (h < tmin) {
            tmin = h;
            nor = normalize(ro + h * rd - sc); // 法線
            occ = 0.5 + 0.5 * nor.y;           // yが大きい(上向きほど明るい
        }

        //球面の法線をinverseSFに渡し、その方向上に最も近い螺旋状の点のインデックスとその点までの距離を返す
        vec2 fi = inverseSF(nor);

        //---------------------------
        // 色の計算
        //---------------------------
        /*
        hash1(fi.x * 13.0)=点インデックスに13.0をかけてからhash1を渡し
        点ごとに異なる乱数(0...1)を得る。13.0はシードの変化を増やす値
        hash1(...) * 3.0 + 1.0=乱数を*3.0で振幅を広げ、+1.0で位相をずらす
        +vec3(0.0, 1.0, 1.0)=sin(...)にRGB用の位相オフセットを与えている
        Rはそのまま、GとBは+1.0の位相ずらしがされる
        */
        col = 0.5 + 0.5 * sin(
            hash1(fi.x * 13.0) * 3.0 + 1.0 + vec3(0.0, 1.0, 1.0)
        );
        col *= smoothstep(0.02, 0.03, fi.y);
        col *= mix(1.0, 1.0 - smoothstep(0.12, 0.125, fi.y),
                    smoothstep(-0.9, 0.1, sin(u_time)));
        col *= 1.0 + 0.1 * sin(100.0 * radius * fi.y);
        col *= 1.5;
    }

    /*
    ↑ y
    |
    |   camera (ro)   ← レイの始点
    * 
     \
      \
       \   rd (レイの方向)
        \      .   ← intersection P1 (入射): t1(小さいt)
         \    / \
          \  /   \
            ---    ← sc (radius 1)
          /  \     ← exit point P2 (出射): t2(大きいt)
    レイが球の内側から始まると、P1は負、P2は正になる
    */


    /*
    見つかったfiに対して、fiを中心に球を形成し、またfiを見つける。これを繰り返す。
    再起呼び出しができないから、ループの中にループという形で書く
    */

    
    //---------------------------
    // 背景フェード
    //---------------------------
    if (tmin < 100.0) {
        pos = ro + tmin * rd;
        col *= occ;
        col = mix(col, vec3(1.0), 1.0 - exp(-0.003 * tmin * tmin));
    }

    //---------------------------
    // ガンマ補正
    //---------------------------
    col = sqrt(col);
    fragColor = vec4(col, 1.0);
}
